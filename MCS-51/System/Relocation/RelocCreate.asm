;* Yggdrasil (TM) Core Operating System (MCS-51): Relocatable Code Library
;* Copyright (C) DeRemee Systems, IXE Electronics LLC
;* Portions copyright IXE Electronics LLC, Republic Robotics,
;* FemtoLaunch, FemtoSat, FemtoTrack, Weland
;* This work is made available under the Creative Commons
;* Attribution-NonCommercial-ShareAlike 4.0 International License.
;* To view a copy of this license, visit
;* http://creativecommons.org/licenses/by-nc-sa/4.0/.

;CREATES A NEW RELOCATION TABLE ENTRY
;ON ENTRY:
;	R0		= CODE BASE ADDRESS LSB
;	R1		= CODE BASE ADDRESS MSB
;	R2		= CODE PAGE
;	R3		= FLASH DATA BASE ADDRESS LSB
;	R4		= FLASH DATA BASE ADDRESS MSB
;	R5		= FLASH DATA PAGE
;	R6		= BPB INDEX
;ON RETURN:
;	C = 0 IF SUCCESS
;		A = ENTRY'S RELOCATION TABLE ID
;	C = 1 IF FAIL
;		A	= ERROR CODE
;	R0		= VALUE ON ENTRY
;	R1		= VALUE ON ENTRY
;	R2		= VALUE ON ENTRY
;	R3		= VALUE ON ENTRY
;	R4		= VALUE ON ENTRY
;	R5		= VALUE ON ENTRY
;	R6		= VALUE ON ENTRY
LRELOCCREATE:
  ;SAVE REGISTERS
  PUSH	B
  PUSH	DPH
  PUSH	DPL
  MOV		A, R0
  PUSH	ACC
  ;LOAD RELOCATION TABLE CONTROL BLOCK ADDRESS
  MOV		DPTR, #RELOC_ADR
  CALL	DPTRWLDXDNN
  ;LOAD MAXIMUM ENTRY COUNT
  MOV		B, #RELOC_OFFS_MAX_ENTRIES
  CALL	LDPTRBMRMDON
  MOV		R0, A
  ;POINT TO START OF RELOCATION TABLE
  MOV		A, #RELOC_OFFS_TABLE
  CALL	LDPTRBADD
  ;SETUP
  MOV		B, #RELOC_TBL_OFFS_FLAGS
LRELOCCREATEA:
  ;CHECK FOR FREE RELOCATION TABLE ENTRY
  CALL	LDPTRBMRMDON
  JNB		RELOC_BIT_TBL_PRESENT, LRELOCCREATEC
  ;ENTRY IS PRESENT, POINT TO NEXT ENTRY
  MOV		A, #RELOC_ENTRY_SIZE
  CALL	LDPTRBADD
  ;CHECK FOR LAST ENTRY
  DJNZ	R0, LRELOCCREATEA
  ;NO FREE ENTRY FOUND
  SETB	C
  MOV		R0, #0x01
LRELOCCREATEB:
  ;RESTORE REGISTERS AND RETURN
  POP		ACC
  POP		DPL
  POP		DPH
  POP		B
  XCH		A, R0
  RET
LRELOCCREATEC:
  ;FREE ENTRY FOUND, MARK ENTRY AS PRESENT
  SETB	RELOC_BIT_TBL_PRESENT
  CALL	LDPTRBMMRDON
  ;STORE RELOCATION BASE ADDRESS IN RELOCATION TABLE
  POP		ACC
  PUSH	ACC
  MOV		B, #RELOC_TBL_OFFS_ADR_LSB
  CALL	LDPTRBMMRDON
  MOV		A, R1
  MOV		B, #RELOC_TBL_OFFS_ADR_MSB
  CALL	LDPTRBMMRDON
  MOV		A, R2
  MOV		B, #RELOC_TBL_OFFS_ADR_PAGE
  CALL	LDPTRBMMRDON
  MOV		A, R3
  MOV		B, #RELOC_TBL_OFFS_FDA_LSB
  CALL	LDPTRBMMRDON
  MOV		A, R4
  MOV		B, #RELOC_TBL_OFFS_FDA_MSB
  CALL	LDPTRBMMRDON
  MOV		A, R5
  MOV		B, #RELOC_TBL_OFFS_FDA_PAGE
  CALL	LDPTRBMMRDON
  MOV		A, R6
  MOV		B, #RELOC_TBL_OFFS_BPB_INDX
  CALL	LDPTRBMMRDON
  MOV		R0, #0x00
  CLR		C
  JMP		LRELOCCREATEB
