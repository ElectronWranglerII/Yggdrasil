;* Yggdrasil (TM) Core Operating System (68K): String Library
;* Copyright (C) DeRemee Systems, IXE Electronics LLC
;* Portions copyright IXE Electronics LLC, Republic Robotics,
;* FemtoLaunch, FemtoSat, FemtoTrack, Weland
;* This work is made available under the Creative Commons
;* Attribution-NonCommercial-ShareAlike 4.0 International License.
;* To view a copy of this license, visit
;* http://creativecommons.org/licenses/by-nc-sa/4.0/.

;SEARCHES THE STRING FOR A SUBSTRING
;ON ENTRY:
;	D0.B	= TERMINATOR
;	A0		= STRING ADDRESS
;	A1		= SUBSTRING ADDRESS
;	[A0]	= STRING
;	[A1]	= SUBSTRING
;ON RETURN:
;	D0.B	= VALUE ON ENTRY
;	[A0]	= VALUE ON ENTRY
;	[A1]	= VALUE ON ENTRY
;	D7.B	= 0x00 IF SUBSTRING NOT FOUND
;		A0		= VALUE ON ENTRY
;	D7.B	= 0xFF IF SUBSTRING FOUND
;		A0		= ADDRESS OF FOUND SUBSTRING
STRFINDSTR:
	;SAVE REGISTERS
	MOVE.L	D1, -(SP)
	MOVE.L	A0, -(SP)
STRFINDSTRA:
	MOVE.L	A1, -(SP)
STRFINDSTRB:
	;LOAD STRING CHARACTER
	MOVE.B	(A0), D1
	;CHARACTER == TERMINATOR?
	CMP.B	D0, D1
	BEQ		STRFINDSTRF
	;STRING CHARACTER == SUBSTRING CHARACTER?
	CMP.B	(A1), D1
	BEQ		STRFINDSTRC
	;POINT TO NEXT STRING CHARACTER
	ADDQ.L	#$1, A0
	BRA		STRFINDSTRB
STRFINDSTRC:
	;SAVE STRING ADDRESS
	MOVE.L	A0, -(SP)
	;POINT TO NEXT STRING CHARACTER
	ADDQ.L	#$1, A0
	;POINT TO NEXT SUBSTRING CHARACTER
	ADDQ.L	#$1, A1
STRFINDSTRD:
	;SUBSTRING CHARACTER == TERMINATOR?
	CMP.B	(A1), D0
	BEQ		STRFINDSTRE
	;STRING CHARACTER == SUBSTRING CHARACTER?
	CMPM.B	(A0)+, (A1)+
	BEQ		STRFINDSTRD
	;RESTORE SAVED STRING ADDRESS
	MOVE.L	(SP)+, A0
	ADDQ.L	#$1, A0
	;RESTORE SUBSTRING ADDRESS
	MOVE.L	(SP)+, A1
	BRA		STRFINDSTRA
STRFINDSTRE:
	;RESTORE REGISTERS & RETURN
	SEQ		D7
	MOVE.L	(SP)+, A0
	MOVE.L	(SP)+, A1
	ADDQ.L	#$4, A7
	MOVE.L	(SP)+, D1
	RTS
STRFINDSTRF:
	CLR.B	D7
	MOVE.L	(SP)+, A1
	MOVE.L	(SP)+, A0
	MOVE.L	(SP)+, D1
	RTS
