;* Yggdrasil (TM) Core Operating System (x86): Character Library
;* Copyright (C) DeRemee Systems, IXE Electronics LLC
;* Portions copyright IXE Electronics LLC, Republic Robotics,
;* FemtoLaunch, FemtoSat, FemtoTrack, Weland
;* This work is made available under the Creative Commons
;* Attribution-NonCommercial-ShareAlike 4.0 International License.
;* To view a copy of this license, visit
;* http://creativecommons.org/licenses/by-nc-sa/4.0/.


;CONVERTS A SINGLE DIGIT HEX VALUE TO A CHARACTER
;ON ENTRY:
;	AL = SINGLE DIGIT HEX VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS HEXADECIMAL
;	C = 1 IF VALUE IS NOT HEXADECIMAL
CHARFROMHEX:
	;VALUE > 0x0F?
	CMP		AL, 0x10
	JNC		CHARFROMHEXB
	;CONVERT TO CHARACTER
	ADD		AL, 0x30
	;CHARACTER > "9"?
	CMP		AL, 0x3A
	JC		CHARFROMHEXA
	;ADD 0x07 TO CHARACTER
	ADD		AL, 0x07
CHARFROMHEXA:
	CLC
	RET
CHARFROMHEXB:
	XOR		AL, AL
	STC
	RET


;CONVERTS A SINGLE DIGIT INTEGER TO A CHARACTER
;ON ENTRY:
;	AL = SINGLE DIGIT INTEGER
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS AN INTEGER
;	C = 1 IF VALUE IS NOT AN INTEGER
CHARFROMINT:
	;VALUE > 0x09?
	CMP		AL, 0x0A
	JNC		CHARFROMINTA
	;CONVERT TO CHARACTER
	ADD		AL, 0x30
	RET
CHARFROMINTA:
	XOR		AL, AL
	STC
	RET


;CONVERTS THE HEX CHARACTER IN AL TO AN INTEGER
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	C = 0 IF CHARACTER IS HEX
;			AL = INTEGER VALUE
;	C = 1 IF CHARACTER IS NOT HEX
;			AL = 0x00
CHARHEXTOINT:
	;CHECK FOR LESS THAN "0"
	SUB		AL, 0x30
	JC		CHARHEXTOINTC
	;CHECK FOR GREATER THAN "9"
	CMP		AL, 0x0A
	JNC		CHARHEXTOINTB
CHARHEXTOINTA:
	CLC
	RET
CHARHEXTOINTB:
	;SUBTRACT 0x07
	SUB		AL, 0x07
	;CHECK FOR LESS THAN "A"
	CMP		AL, 0x0A
	JC		CHARHEXTOINTC
	;CHECK FOR GREATER THAN "F"
	CMP		AL, 0x10
	JC		CHARHEXTOINTA
	;CONVERT TO UPPERCASE
	SUB		AL, 0x20
	JC		CHARHEXTOINTC
	;CHECK FOR LESS THAN "A"
	CMP		AL, 0x0A
	JC		CHARHEXTOINTC
	;CHECK FOR GREATER THAN "F"
	CMP		AL, 0x10
	JC		CHARHEXTOINTA
CHARHEXTOINTC:
	XOR		AL, AL
	STC
	RET


;RETURNS TRUE IF VALUE IS AN ARITHMETIC OPERATOR
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT AN ARITHMETIC OPERATOR
;	C = 1 IF VALUE IS AN ARITHMETIC OPERATOR
CHARISARITH:
	;CHECK FOR FACTORAL "!"
	CMP		AL, 0x21
	JZ		CHARISARITHA
	;CHECK FOR MODULO "%"
	CMP		AL, 0x25
	JZ		CHARISARITHA
	;CHECK FOR MULTIPLICATION "*"
	CMP		AL, 0x2A
	JZ		CHARISARITHA
	;CHECK FOR ADDITION "+"
	CMP		AL, 0x2B
	JZ		CHARISARITHA
	;CHECK FOR SUBTRACTION "-"
	CMP		AL, 0x2D
	JZ		CHARISARITHA
	;CHECK FOR DIVISION "/"
	CMP		AL, 0x2F
	JZ		CHARISARITHA
	;CHECK FOR EQUALS "="
	CMP		AL, 0x3D
	JZ		CHARISARITHA
	;CHECK FOR EXPONENT "^"
	CMP		AL, 0x5E
	JNZ		CHARISARITHB
CHARISARITHA:
	STC
	RET
CHARISARITHB:
	CLC
	RET


;RETURNS TRUE IF VALUE IS A CLOSING BRACKET
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A CLOSING BRACKET
;	C = 1 IF VALUE IS AN A CLOSING BRACKET
CHARISBRACC:
	;CHECK FOR ")"
	CMP		AL, 0x29
	JZ		CHARISBRACCA
	;CHECK FOR "]"
	CMP		AL, 0x5D
	JZ		CHARISBRACCA
	;CHECK FOR "}"
	CMP		AL, 0x7D
	JNZ		CHARISBRACCB
CHARISBRACCA:
	STC
	RET
CHARISBRACCB:
	CLC
	RET


;RETURNS TRUE IF VALUE IS AN OPENING BRACKET
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT AN OPENING BRACKET
;	C = 1 IF VALUE IS AN AN OPENING BRACKET
CHARISBRACO:
	;CHECK FOR "("
	CMP		AL, 0x28
	JZ		CHARISBRACOA
	;CHECK FOR "["
	CMP		AL, 0x5B
	JZ		CHARISBRACOA
	;CHECK FOR "{"
	CMP		AL, 0x7B
	JNZ		CHARISBRACOB
CHARISBRACOA:
	STC
	RET
CHARISBRACOB:
	CLC
	RET


;RETURNS TRUE IF VALUE IS A NON-PRINTING CHARACTER
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A NON-PRINTING CHARACTER
;	C = 1 IF VALUE IS A NON-PRINTING CHARACTER
CHARISNPC:
	;CHECK FOR LESS THAN 0x20
	CMP		AL, 0x20
	JC		CHARISNPCA
	;CHECK FOR 0x7F
	CMP		AL, 0x7F
	JZ		CHARISNPCA
	CLC
	RET
CHARISNPCA:
	STC
	RET


;RETURNS TRUE IF VALUE IS A HEX CHARACTER
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A HEX CHARACTER
;	C = 1 IF VALUE IS A HEX CHARACTER
CHARISHEX:
	;CHECK FOR LESS THAN "0"
	CMP		AL, 0x30
	JC		CHARISHEXA
	;CHECK FOR GREATER THAN "9"
	CMP		AL, 0x3A
	JC		CHARISHEXB
	;CHECK FOR LESS THAN "A"
	CMP		AL, 0x41
	JC		CHARISHEXA
	;CHECK FOR GREATER THAN "F"
	CMP		AL, 0x47
	JC		CHARISHEXB
CHARISHEXA:
	CLC
CHARISHEXB:
	RET


;RETURNS TRUE IF VALUE IS A LETTER
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A LETTER
;	C = 1 IF VALUE IS A LETTER
CHARISLETTER:
	;CHECK FOR LESS THAN "A"
	CMP		AL, 0x41
	JC		CHARISLETTERA
	;CHECK FOR GREATER THAN "Z"
	CMP		AL, 0x5B
	JNC		CHARISLETTERB
	;CHECK FOR LESS THAN "a"
	CMP		AL, 0x61
	JC		CHARISLETTERA
	;CHECK FOR GREATER THAN "z"
	CMP		AL, 0x7B
	JNC		CHARISLETTERB
	RET
CHARISLETTERA:
	CLC
CHARISLETTERB:
	RET


;RETURNS TRUE IF VALUE IS A LOGICAL OPERATOR
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A LOGICAL OPERATOR
;	C = 1 IF VALUE IS AN A LOGICAL OPERATOR
CHARISLOGIC:
	;CHECK FOR NOT "!"
	CMP		AL, 0x21
	JZ		CHARISLOGICA
	;CHECK FOR AND "&"
	CMP		AL, 0x26
	JZ		CHARISLOGICA
	;CHECK FOR XOR "^"
	CMP		AL, 0x5E
	JZ		CHARISLOGICA
	;CHECK FOR OR "|"
	CMP		AL, 0x7C
	JNZ		CHARISLOGICB
CHARISLOGICA:
	STC
	RET
CHARISLOGICB:
	CLC
	RET


;RETURNS TRUE IF VALUE IS A LOWERCASE LETTER
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A LOWERCASE LETTER
;	C = 1 IF VALUE IS A LOWERCASE LETTER
CHARISLOWER:
	;CHECK FOR LESS THAN "a"
	CMP		AL, 0x61
	JC		CHARISLOWERA
	;CHECK FOR GREATER THAN "z"
	CMP		AL, 0x7B
	JC		CHARISLOWERB
CHARISLOWERA:
	CLC
	RET
CHARISLOWERB:
	STC
	RET


;RETURNS TRUE IF VALUE IS A NUMBER
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A NUMBER
;	C = 1 IF VALUE IS A NUMBER
CHARISNUM:
	;CHECK FOR LESS THAN "0"
	CMP		AL, 0x30
	JC		CHARISNUMA
	;CHECK FOR GREATER THAN "9"
	CMP		AL, 0x3A
	JNC		CHARISNUMB
	RET
CHARISNUMA:
	CLC
CHARISNUMB:
	RET


;RETURNS TRUE IF VALUE IS PUNCTUATION
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT PUNCTUATION
;	C = 1 IF VALUE IS PUNCTUATION
CHARISPUNCT:
	;CHECK FOR "!"
	CMP		AL, 0x21
	JZ		CHARISPUNCTA
	;CHECK FOR "."
	CMP		AL, 0x2E
	JZ		CHARISPUNCTA
	;CHECK FOR "?"
	CMP		AL, 0x3F
	JNZ		CHARISPUNCTB
CHARISPUNCTA:
	STC
	RET
CHARISPUNCTB:
	CLC
	RET


;RETURNS TRUE IF VALUE IS A RELATIONAL OPERATOR
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT A RELATIONAL OPERATOR
;	C = 1 IF VALUE IS A RELATIONAL OPERATOR
CHARISREL:
	;CHECK FOR LESS THAN "<"
	CMP		AL, 0x3C
	JC		CHARISRELA
	;CHECK FOR GREATER THAN ">"
	CMP		AL, 0x3F
	JNC		CHARISRELB
	RET
CHARISRELA:
	CLC
CHARISRELB:
	RET


;RETURNS TRUE IF VALUE IS AN UPPERCASE LETTER
;ON ENTRY:
;	AL = VALUE
;ON RETURN:
;	AL = VALUE ON ENTRY
;	C = 0 IF VALUE IS NOT AN UPPERCASE LETTER
;	C = 1 IF VALUE IS AN UPPERCASE LETTER
CHARISUPPER:
	;CHECK FOR LESS THAN "A"
	CMP		AL, 0x41
	JC		CHARISUPPERA
	;CHECK FOR GREATER THAN "Z"
	CMP		AL, 0x5B
	JC		CHARISUPPERB
CHARISUPPERA:
	CLC
	RET
CHARISUPPERB:
	STC
	RET


;CONVERTS THE ASCII CHARACTER IN AL TO AN INTEGER
;ON ENTRY:
;	AL = ASCII VALUE
;ON RETURN:
;	C = 0 IF CHARACTER IS A NUMBER
;			AL = INTEGER VALUE
;	C = 1 IF CHARACTER IS NOT A NUMBER
;			AL = 0x00
CHARTOINT:
	;CHECK FOR LESS THAN "0"
	SUB		AL, 0x30
	JC		CHARTOINTA
	;CHECK FOR GREATER THAN "9"
	PUSH	AX
	SUB		AL, 0x0A
	POP		AX
	JNC		CHARTOINTA
	CLC
	RET
CHARTOINTA:
	XOR		AL, AL
	STC
	RET


;CONVERTS AN UPPERCASE LETTER TO AN LOWERCASE LETTER
;ON ENTRY:
;	AL = CHARACTER
;ON RETURN:
;	C = 0 IF CHARACTER WAS UPPERCASE
;		AL = CONVERTED VALUE
;	C = 1 IF CHARACTER WAS NOT UPPERCASE
;		AL = VALUE ON ENTRY
CHARTOLOWER:
	;CHECK FOR LESS THAN "A"
	CMP		AL, 0x41
	JC		CHARTOLOWERB
	;CHECK FOR GREATER THAN "Z"
	CMP		AL, 0x5B
	JNC		CHARTOLOWERA
	;CONVERT TO LOWERCASE
	ADD		AL, 0x20
	RET
CHARTOLOWERA:
	STC
CHARTOLOWERB:
	RET


;CONVERTS A LOWERCASE LETTER TO AN UPPERCASE LETTER
;ON ENTRY:
;	AL = CHARACTER
;ON RETURN:
;	C = 0 IF CHARACTER WAS LOWERCASE
;		AL = CONVERTED VALUE
;	C = 1 IF CHARACTER WAS NOT LOWERCASE
;		AL = VALUE ON ENTRY
CHARTOUPPER:
	;CHECK FOR LESS THAN "a"
	CMP		AL, 0x61
	JC		CHARTOUPPERB
	;CHECK FOR GREATER THAN "z"
	CMP		AL, 0x7B
	JNC		CHARTOUPPERA
	;CONVERT TO UPPERCASE
	SUB		AL, 0x20
	RET
CHARTOUPPERA:
	STC
CHARTOUPPERB:
	RET
